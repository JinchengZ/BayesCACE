model{
for (i in 1:n) {

prob[i, 1] <- (pi_n[i]*(1-s1[i]) + pi_c[i]*(1-v1[i]))
prob[i, 2] <- (pi_n[i]*s1[i] + pi_c[i]*v1[i])
prob[i, 3] <- (pi_a[i]*(1-b1[i]))
prob[i, 4] <- (pi_a[i]*b1[i])
prob[i, 5] <- (pi_n[i]*(1-s1[i]))
prob[i, 6] <- (pi_n[i]*s1[i])
prob[i, 7] <- (pi_c[i]*(1-u1[i])+pi_a[i]*(1-b1[i]))
prob[i, 8] <- (pi_c[i]*u1[i]+pi_a[i]*b1[i])

R[i, 1:4] ~ dmulti(prob[i, 1:4], N0[i])
R[i, 5:8] ~ dmulti(prob[i, 5:8], N1[i])

probit(u1[i]) <- alpha_u + delta_u[i]
delta_u[i] ~ dnorm(0, tau_u)
probit(v1[i]) <- alpha_v + delta_v[i]
delta_v[i] ~ dnorm(0, tau_v)

n[i] <- alpha_n + delta_n[i]
delta_n[i] ~ dnorm(0, tau_n)
a[i] <- alpha_a + delta_a[i]
delta_a[i] ~ dnorm(0, tau_a)
pi_n[i] <- exp(n[i])/(1+exp(n[i])+exp(a[i]))
pi_a[i] <- exp(a[i])/(1+exp(n[i])+exp(a[i]))
pi_c[i] <- 1-pi_a[i]-pi_n[i]

logit(s1[i]) <- alpha_s1 + delta_s[i]
delta_s[i] ~ dnorm(0, tau_s)
logit(b1[i]) <- alpha_b1 + delta_b[i]
delta_b[i] ~ dnorm(0, tau_b)
} 

CACE <- phi(alpha_u/sqrt(1+sigma_u^2))-phi(alpha_v/sqrt(1+sigma_v^2))

pin <- exp(alpha_n)/(1+exp(alpha_n)+exp(alpha_a))
pia <- exp(alpha_a)/(1+exp(alpha_n)+exp(alpha_a))
pic <- 1-pia-pin
u1out <- phi(alpha_u/sqrt(1+sigma_u^2))
v1out <- phi(alpha_v/sqrt(1+sigma_v^2))
s1out <- ilogit(alpha_s1)
b1out <- ilogit(alpha_b1)

# priors
alpha_n ~  dnorm(0, 0.16)
tau_n ~ dgamma(2, 2)
sigma_n <- 1/sqrt(tau_n)
alpha_a ~ dnorm(0, 0.16)
tau_a ~ dgamma(2, 2)
sigma_a <- 1/sqrt(tau_a)

alpha_s1 ~  dnorm(0, 0.25)
tau_s ~ dgamma(2, 2)
sigma_s <- 1/sqrt(tau_s)
alpha_b1 ~  dnorm(0, 0.25)
tau_b ~ dgamma(2, 2)
sigma_b <- 1/sqrt(tau_b)

alpha_u ~  dnorm(0, 0.25)
tau_u ~ dgamma(2, 2)
sigma_u <- 1/sqrt(tau_u)
alpha_v ~  dnorm(0, 0.25)
tau_v ~ dgamma(2, 2)
sigma_v <- 1/sqrt(tau_v)
}
